      ******************************************************************
      * Author: Melanie Mombru
      * Date: 2022 10
      * Ejercicio 1.4 - Serie 7

      ******************************************************************
       IDENTIFICATION DIVISION.
      *-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
       PROGRAM-ID. PROBLEMM.
       ENVIRONMENT DIVISION.
      *-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
       CONFIGURATION SECTION.
      *-----------------------
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

       SELECT ENTRADA-SERVICIOS    ASSIGN TO DISK 'SERVICIOS.DAT'
                                   ORGANIZATION IS LINE SEQUENTIAL
                                   FILE STATUS IS WSS-FS-SERVICIOS.

       SELECT ENTRADA-CUENTAS      ASSIGN TO DISK 'CUENTAS.DAT'
                                   ORGANIZATION IS LINE SEQUENTIAL
                                   FILE STATUS IS WSS-FS-CUENTAS.

       SELECT SALIDA-RECHAZOS      ASSIGN TO DISK 'RECHAZOS.DAT'
                                   ORGANIZATION IS LINE SEQUENTIAL
                                   FILE STATUS IS WSS-FS-RECHAZOS.

       SELECT SALIDA-INCIDENCIAS   ASSIGN TO DISK 'INCIDENCIAS.DAT'
                                   ORGANIZATION IS LINE SEQUENTIAL
                                   FILE STATUS IS WSS-FS-INCIDENCIAS.

       SELECT SALIDA-CUENTAS      ASSIGN TO DISK 'CUENTAS-NUEVO.DAT'
                                   ORGANIZATION IS LINE SEQUENTIAL
                                   FILE STATUS IS WSS-FS-CUENTAS-NUEVO.
                       
      *-----------------------
       DATA DIVISION.
       FILE SECTION.

       FD ENTRADA-SERVICIOS.
       01 REG-SERVICIOS.
           05 REG-NRO-CLIENTE-S            PIC 9(02).
           05 REG-COD-SERVICIO-S           PIC X(03).
           05 REG-MONTO-S                  PIC 9(05)V9(02).

       FD ENTRADA-CUENTAS.
       01 REG-CUENTAS.
           05 REG-NRO-CLIENTE              PIC 9(02).
           05 REG-NOMBRE                   PIC X(30).
           05 REG-SALDO                    PIC 9(05)V9(02).

       FD SALIDA-RECHAZOS.
       01 REG-RECHAZOS.
           05 REG-NRO-CLIENTE-R            PIC 9(02).
           05 REG-NOMBRE-R                 PIC X(30).
           05 REG-SALDO-R                  PIC 9(05)V9(02).
           05 REG-MONTO-R                  PIC 9(05)V9(02).

       FD SALIDA-INCIDENCIAS.
       01 REG-INCIDENCIAS.
           05 REG-NRO-CLIENTE-I            PIC 9(02).
           05 REG-TABLA-I                  PIC X(30).

       FD SALIDA-CUENTAS.
       01 REG-CUENTAS-NUEVO.
           05 REG-NRO-CLIENTE-C            PIC 9(02).
           05 REG-NOMBRE-C                 PIC X(30).
           05 REG-SALDO-C                  PIC 9(05)V9(02).
      *-----------------------
       WORKING-STORAGE SECTION.
       01 WSS-FS-SERVICIOS                 PIC X(02).
           88 WSS-FS-SERVICIOS-OK          VALUE '00'.
           88 WSS-FS-SERVICIOS-EOF         VALUE '10'.

       01 WSS-FS-CUENTAS                   PIC X(02).
           88 WSS-FS-CUENTAS-OK            VALUE '00'.
           88 WSS-FS-CUENTAS-EOF           VALUE '10'.

       01 WSS-FS-RECHAZOS                  PIC X(02).
           88 WSS-FS-RECHAZOS-OK           VALUE '00'.
           88 WSS-FS-RECHAZOS-EOF          VALUE '10'.

       01 WSS-FS-INCIDENCIAS               PIC X(02).
           88 WSS-FS-INCIDENCIAS-OK        VALUE '00'.
           88 WSS-FS-INCIDENCIAS-EOF       VALUE '10'.

       01 WSS-FS-CUENTAS-NUEVO             PIC X(02).
           88 WSS-FS-CUENTAS-NUEVO-OK      VALUE '00'.
           88 WSS-FS-CUENTAS-NUEVO-EOF     VALUE '10'.

       01 WSV-CONT-SERVICIOS               PIC S9(4) COMP.
       01 WSV-CONT-CUENTAS                 PIC S9(4) COMP.
       01 WSV-CONT-RECHAZOS                PIC S9(4) COMP.
       01 WSV-CONT-INCIDENCIAS             PIC S9(4) COMP.
       01 WSV-CONT-CUENTAS-NUEVO           PIC S9(4) COMP.

       01 WSV-CUENTAS.
           05 WSV-NRO-CLIENTE              PIC 9(02).
           05 WSV-NOMBRE                   PIC X(30).
           05 WSV-SALDO                    PIC 9(05)V9(02).

       01 WSV-SERVICIOS.
           05 WSV-NRO-CLIENTE-S            PIC 9(02).
           05 WSV-COD-SERVICIO-S           PIC X(03).
           05 WSV-MONTO-S                  PIC 9(05)V9(02).

      *-----------------------
       PROCEDURE DIVISION.
       0000-PROCESO.
           PERFORM 10000-ABRIR-ARCHIVOS
           PERFORM 20000-APAREO
           PERFORM 30000-CERRAR-ARCHS
           PERFORM FIN-DEL-PROGRAMA.
           
      *ABRIR ARCHIVOS A UTILIZAR
       10000-ABRIR-ARCHIVOS.
           PERFORM 11000-ABRIR-ARCH-SERVICIOS
           PERFORM 12000-ABRIR-ARCH-CUENTAS
           PERFORM 13000-ABRIR-ARCH-RECHAZOS
           PERFORM 14000-ABRIR-ARCH-INCIDENCIAS
           PERFORM 15000-ABRIR-ARCH-CUENTS-NUEVO.

      *ABRIR ARCHIVO DE SERVICIOS
       11000-ABRIR-ARCH-SERVICIOS.
           MOVE 0 TO WSV-CONT-SERVICIOS
           OPEN INPUT ENTRADA-SERVICIOS
           IF NOT WSS-FS-SERVICIOS-OK
               DISPLAY 'ERROR EN EL OPEN DE ARCHIVO DE SERVICIOS'
               DISPLAY 'FILE STATUS ' WSS-FS-SERVICIOS
               PERFORM FIN-DEL-PROGRAMA
           END-IF.

      *ABRIR ARCHIVO DE CUENTAS
       12000-ABRIR-ARCH-CUENTAS.
           MOVE 0 TO WSV-CONT-CUENTAS
           OPEN INPUT ENTRADA-CUENTAS
           IF NOT WSS-FS-CUENTAS-OK
               DISPLAY 'ERROR EN EL OPEN DE ARCHIVO DE CUENTAS'
               DISPLAY 'FILE STATUS ' WSS-FS-CUENTAS
               PERFORM FIN-DEL-PROGRAMA
           END-IF.

      *ABRIR ARCHIVO DE RECHAZOS
       13000-ABRIR-ARCH-RECHAZOS.
           MOVE 0 TO WSV-CONT-RECHAZOS
           OPEN OUTPUT SALIDA-RECHAZOS
           IF NOT WSS-FS-RECHAZOS-OK
               DISPLAY 'ERROR EN EL OPEN DE ARCHIVO DE RECHAZOS'
               DISPLAY 'FILE STATUS ' WSS-FS-RECHAZOS
               PERFORM FIN-DEL-PROGRAMA
           END-IF.

      *ABRIR ARCHIVO DE INCIDENCIAS
       14000-ABRIR-ARCH-INCIDENCIAS.
           MOVE 0 TO WSV-CONT-INCIDENCIAS
           OPEN OUTPUT SALIDA-INCIDENCIAS
           IF NOT WSS-FS-INCIDENCIAS-OK
               DISPLAY 'ERROR EN EL OPEN DE ARCHIVO DE INCIDENCIAS'
               DISPLAY 'FILE STATUS ' WSS-FS-INCIDENCIAS
               PERFORM FIN-DEL-PROGRAMA
           END-IF.

      *ABRIR ARCHIVO DE CUENTAS NUEVO
       15000-ABRIR-ARCH-CUENTS-NUEVO.
           MOVE 0 TO WSV-CONT-CUENTAS-NUEVO
           OPEN OUTPUT SALIDA-CUENTAS
           IF NOT WSS-FS-CUENTAS-NUEVO-OK
               DISPLAY 'ERROR EN EL OPEN DE ARCHIVO DE CUENTAS NUEVO'
               DISPLAY 'FILE STATUS ' WSS-FS-CUENTAS-NUEVO
               PERFORM FIN-DEL-PROGRAMA
           END-IF.

      *REALIZAR APAREO DE UNO A MUCHOS
       20000-APAREO.
           PERFORM 21000-LEER-ARCH-SERVICIOS
           PERFORM 22000-LEER-ARCH-CUENTAS
           PERFORM UNTIL WSS-FS-SERVICIOS-EOF AND WSS-FS-CUENTAS-EOF
               IF REG-NRO-CLIENTE = REG-NRO-CLIENTE-S
      *        ACTUALIZAR CUENTA DEL CLIENTE
                   MOVE REG-CUENTAS TO WSV-CUENTAS
                   MOVE REG-SERVICIOS TO WSV-SERVICIOS
                   PERFORM 25100-ACTUALIZAR-SALDO-CLIENTE 
                   UNTIL ((WSS-FS-SERVICIOS-EOF AND 
                   WSS-FS-CUENTAS-EOF) OR
                   WSV-NRO-CLIENTE NOT= REG-NRO-CLIENTE-S) 
      *            ESCRIBIR CLIENTE EN ARCHIVO CUENTAS
                   PERFORM 25000-ESCRIBIR-ARCH-CUENTS-N
                   PERFORM 22000-LEER-ARCH-CUENTAS
               ELSE
      *            NRO CLIENTE NO SE ENCUENTRA EN EL ARCHIVO DE SERVICIO
                   IF REG-NRO-CLIENTE < REG-NRO-CLIENTE-S
                       MOVE REG-CUENTAS TO WSV-CUENTAS
                       PERFORM 25000-ESCRIBIR-ARCH-CUENTS-N
                       PERFORM 24100-ESCRIBIR-INCI-SERV
                       PERFORM 22000-LEER-ARCH-CUENTAS
                   ELSE
      *            NRO CLIENTE NO SE ENCUENTRA EN EL ARCHIVO DE CUENTAS
                       PERFORM 24200-ESCRIBIR-INCI-CUENT
                       PERFORM 21000-LEER-ARCH-SERVICIOS
                   END-IF
               END-IF
           END-PERFORM.

      *LEER SERVICIO, SI ES EOF LO IGUALA A HIGH VALUE
       21000-LEER-ARCH-SERVICIOS.
           READ ENTRADA-SERVICIOS
           IF WSS-FS-SERVICIOS-OK
               ADD 1 TO WSV-CONT-SERVICIOS
           ELSE 
               IF NOT WSS-FS-SERVICIOS-EOF
                   MOVE HIGH-VALUE TO REG-SERVICIOS
                   DISPLAY 'ERROR EN EL READ DE ARCHIVO DE SERVICIOS'
                   DISPLAY 'FILE STATUS ' WSS-FS-SERVICIOS
               ELSE
                   MOVE HIGH-VALUE TO REG-SERVICIOS
                   DISPLAY 'EOF ARCHIVO DE SERVICIOS'
               END-IF
           END-IF.

      *LEER CUENTA, SI ES EOF LO IGUALA A HIGH VALUE
       22000-LEER-ARCH-CUENTAS.
           READ ENTRADA-CUENTAS
           IF WSS-FS-CUENTAS-OK
               ADD 1 TO WSV-CONT-CUENTAS
           ELSE 
               IF NOT WSS-FS-CUENTAS-EOF
                   DISPLAY 'ERROR EN EL READ DE ARCHIVO DE CUENTAS'
                   DISPLAY 'FILE STATUS ' WSS-FS-CUENTAS
               ELSE
                   MOVE HIGH-VALUE TO REG-CUENTAS
                   DISPLAY 'EOF ARCHIVO DE CUENTAS'
               END-IF
           END-IF.
       
      *GRABAR EL REGISTRO EN EL ARCHIVO DE RECHAZOS
       23000-ESCRIBIR-ARCHIVO-RECHAZOS.
           WRITE REG-RECHAZOS
           IF WSS-FS-RECHAZOS-OK
               ADD 1 TO WSV-CONT-RECHAZOS
           ELSE 
               DISPLAY 'ERROR EN EL WRITE DE ARCHIVO DE RECHAZOS'
               DISPLAY 'FILE STATUS ' WSS-FS-RECHAZOS
           END-IF.

      *ESCRIBIR RECHAZO
       23100-ESCRIBIR-RECHAZO.
           MOVE REG-NRO-CLIENTE TO REG-NRO-CLIENTE-R
           MOVE REG-NOMBRE TO REG-NOMBRE-R
           MOVE WSV-SALDO TO REG-SALDO-R
           MOVE REG-MONTO-S TO REG-MONTO-R
           PERFORM 23000-ESCRIBIR-ARCHIVO-RECHAZOS.

      *GRABAR EL REGISTRO EN EL ARCHIVO DE INCIDENCIAS
       24000-ESCRIBIR-ARCH-INCI.
           WRITE REG-INCIDENCIAS
           IF WSS-FS-INCIDENCIAS-OK
               ADD 1 TO WSV-CONT-INCIDENCIAS
           ELSE 
               DISPLAY 'ERROR EN EL WRITE DE ARCHIVO DE INCIDENCIAS'
               DISPLAY 'FILE STATUS ' WSS-FS-INCIDENCIAS
           END-IF.

      *ESCRIBIR EL REGISTRO QUE FALTA EN SERVICIOS 
      *EN EL ARCHIVO DE INCIDENCIAS
       24100-ESCRIBIR-INCI-SERV.
           MOVE REG-NRO-CLIENTE TO REG-NRO-CLIENTE-I
           MOVE 'SERVICIOS' TO REG-TABLA-I
           PERFORM 24000-ESCRIBIR-ARCH-INCI.

      *ESCRIBIR EL REGISTRO QUE FALTA EN CUENTAS 
      *EN EL ARCHIVO DE INCIDENCIAS
       24200-ESCRIBIR-INCI-CUENT.
           MOVE REG-NRO-CLIENTE-S TO REG-NRO-CLIENTE-I
           MOVE 'CUENTAS' TO REG-TABLA-I
           PERFORM 24000-ESCRIBIR-ARCH-INCI.

      *GRABAR EL REGISTRO EN EL ARCHIVO DE CUENTAS NUEVO
       25000-ESCRIBIR-ARCH-CUENTS-N.
           WRITE REG-CUENTAS-NUEVO FROM WSV-CUENTAS
           IF WSS-FS-CUENTAS-NUEVO-OK
               ADD 1 TO WSV-CONT-CUENTAS-NUEVO
           ELSE 
               DISPLAY 'ERROR EN EL WRITE DE ARCHIVO DE CUENTAS NUEVO'
               DISPLAY 'FILE STATUS ' WSS-FS-CUENTAS-NUEVO
           END-IF.
       
      *ACTUALIZA EL SALDO DEL CLIENTE SI SU SALDO ES MAYOR O IGUAL AL
      *MONTO, SINO LO ESCRIBE EN RECHAZOS
       25100-ACTUALIZAR-SALDO-CLIENTE.
           IF WSV-SALDO >= REG-MONTO-S
               COMPUTE WSV-SALDO = WSV-SALDO - REG-MONTO-S
           ELSE 
      *        ESCRIBIR RECHAZO
               PERFORM 23100-ESCRIBIR-RECHAZO
           END-IF
           MOVE REG-SERVICIOS TO WSV-SERVICIOS
           PERFORM 21000-LEER-ARCH-SERVICIOS.
       
      *CERRAR ARCHIVOS E INFORMAR
       30000-CERRAR-ARCHS.
           PERFORM 31000-CERRAR-ARCH-SERVICIOS
           PERFORM 32000-CERRAR-ARCH-CUENTAS
           PERFORM 33000-CERRAR-ARCH-RECHAZOS
           PERFORM 34000-CERRAR-ARCH-INCI
           PERFORM 35000-CERRAR-ARCH-CUENT-N.
       
      *CERRAR ARCHIVO DE SERVICIO E INFORMAR
       31000-CERRAR-ARCH-SERVICIOS.
           CLOSE ENTRADA-SERVICIOS
           IF NOT WSS-FS-SERVICIOS-OK
               DISPLAY 'ERROR EN EL CLOSE DE ARCHIVO DE SERVICIOS'
               DISPLAY 'FILE STATUS ' WSS-FS-SERVICIOS
           END-IF
           PERFORM 31100-INFO-CANT-SERV-LEIDOS.

      *CERRAR ARCHIVO DE CUENTAS E INFORMAR
       32000-CERRAR-ARCH-CUENTAS.
           CLOSE ENTRADA-CUENTAS
           IF NOT WSS-FS-CUENTAS-OK
               DISPLAY 'ERROR EN EL CLOSE DE ARCHIVO DE CUETAS'
               DISPLAY 'FILE STATUS ' WSS-FS-CUENTAS
           END-IF
           PERFORM 32100-INFO-CANT-CUENT-LEIDAS.
       
      *CERRAR ARCHIVO DE RECHAZOS E INFORMAR
       33000-CERRAR-ARCH-RECHAZOS.
           CLOSE SALIDA-RECHAZOS
           IF NOT WSS-FS-RECHAZOS-OK
               DISPLAY 'ERROR EN EL CLOSE DE ARCHIVO DE RECHAZOS'
               DISPLAY 'FILE STATUS ' WSS-FS-RECHAZOS
           END-IF
           PERFORM 33100-INFO-CANT-RECH-ESCRITOS.

      *CERRAR ARCHIVO DE INCIDENCIAS E INFORMAR
       34000-CERRAR-ARCH-INCI.
           CLOSE SALIDA-INCIDENCIAS
           IF NOT WSS-FS-INCIDENCIAS-OK
               DISPLAY 'ERROR EN EL CLOSE DE ARCHIVO DE INCIDENCIAS'
               DISPLAY 'FILE STATUS ' WSS-FS-INCIDENCIAS
           END-IF
           PERFORM 34100-INFO-CANT-INCI-ESCRITAS.

      *CERRAR ARCHIVOS DE CUENTAS-NUEVO  E INFORMAR
       35000-CERRAR-ARCH-CUENT-N.
           CLOSE SALIDA-CUENTAS
           IF NOT WSS-FS-CUENTAS-NUEVO-OK
               DISPLAY 'ERROR EN EL CLOSE DE ARCHIVO DE CUENTAS NUEVO'
               DISPLAY 'FILE STATUS ' WSS-FS-CUENTAS
           END-IF
           PERFORM 35100-INFO-CANT-CUENT-N-ESCR.

      *INFORMAR CANTIDAD DE SERVICIOS LEIDOS
       31100-INFO-CANT-SERV-LEIDOS.
           IF WSV-CONT-SERVICIOS = 0
               DISPLAY "ARCHIVO VACIO"
           ELSE    
               DISPLAY "CANTIDAD DE SERVICIOS LEIDOS: "
               WSV-CONT-SERVICIOS
           END-IF.

      *INFORMAR CANTIDAD DE CUENTAS LEIDAS
       32100-INFO-CANT-CUENT-LEIDAS.
           IF WSV-CONT-CUENTAS = 0
               DISPLAY "ARCHIVO VACIO"
           ELSE    
               DISPLAY "CANTIDAD DE CUENTAS LEIDAS: "
               WSV-CONT-CUENTAS
           END-IF.

      *INFORMAR CANTIDAD DE RECHAZOS ESCRITOS
       33100-INFO-CANT-RECH-ESCRITOS.
           IF WSV-CONT-RECHAZOS = 0
               DISPLAY "ARCHIVO VACIO"
           ELSE    
               DISPLAY "CANTIDAD DE RECHAZOS ESCRITOS: "
               WSV-CONT-RECHAZOS
           END-IF.

      *INFORMAR CANTIDAD DE INCIDENCIAS ESCRITOS
       34100-INFO-CANT-INCI-ESCRITAS.
           IF WSV-CONT-INCIDENCIAS = 0
               DISPLAY "ARCHIVO VACIO"
           ELSE    
               DISPLAY "CANTIDAD DE INCIDENCIAS ESCRITAS: "
               WSV-CONT-INCIDENCIAS
           END-IF.

      *INFORMAR CANTIDAD DE CUENTAS NUEVAS ESCRITOS
       35100-INFO-CANT-CUENT-N-ESCR.
           IF WSV-CONT-CUENTAS-NUEVO = 0
               DISPLAY "ARCHIVO VACIO"
           ELSE    
               DISPLAY "CANTIDAD DE CUENTAS NUEVAS ESCRITAS: "
               WSV-CONT-CUENTAS-NUEVO
           END-IF.

      *DA FIN AL PROGRAMA
       FIN-DEL-PROGRAMA.
           STOP RUN.
